       IDENTIFICATION DIVISION.
       PROGRAM-ID PGMCC799.
      ************************************************************
      *                                                          *
      *  PROGRAMA PARA EMITIR TOTAL CTAS ACTIVAS POR SUCURSAL    *
      *  ACTIVAS PPOR SUC Y  TOTAL CTAS ACTIVAS BCO              *
      *  LA ENTRADA CLASIFICADA X SUCURSAL                       *
      *                                                          *
      ************************************************************
      *      MANTENIMIENTO DE PROGRAMA                           *
      ************************************************************
      *  FECHA   *   DETALLE              * COD *
      *******************************************
      *          *                        *     *
      *          *                        *     *
      *******************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
             DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
             SELECT ENTRADA ASSIGN DDENTRA
                    FILE STATUS IS FS-ENTRADA.

             SELECT SALIDA  ASSIGN DDSALIDA
                    FILE STATUS IS FS-SALIDA.

       DATA DIVISION.
       FILE SECTION.
       FD ENTRADA
            BLOCK CONTAINS 0 RECORDS
            RECORDING MODE IS F.

       01 REG-ENTRADA PIC X(50).

       FD SALIDA
            BLOCK CONTAINS 0 RECORDS
            RECORDING MODE IS F.

       01 REG-SALIDA  PIC X(50).


      ******************************************
       WORKING-STORAGE SECTION.
      ******************************************
       77  FILLER        PIC X(26) VALUE '* INICIO WORKING-STORAGE *'.
       01  FS-FSCODES.
           05  FS-ENTRADA                PIC XX.
               88  FS-FIN-E1             VALUE  '10'.
           05  FS-SALIDA                 PIC XX.

       01  WS-VARIABLES.
           05  WS-SUC-ANT                PIC XX.
           05  WS-ACC-BALANCE            PIC S9(09)V99.
           05  WS-ERR-PARRAFO            PIC X(30).
           05  WS-ERR-OPERACION          PIC X(30).
           05  WS-ERR-CODE               PIC X(30).
           05  WS-CONT-SUC               PIC 9(03).
           05  WS-CONT-LEIDOS            PIC 9(03).
           05  WS-CONT-GRABADOS          PIC 9(03).
       01  WS-CONSTANTES.
           05  CONS-CERO                 PIC 9(01)   VALUE 0.
           05  WS-CONS-LINEA             PIC X(50)   VALUE ALL '*'.
      ***************************************
       COPY CPCLIENS.
      ***************************************

      ***************************************
      * LAY-OUT DE CORTE DE CONTROL
      ***************************************
       01  WS-REG-CORTE.
           05  FILLER                PIC X(01)   VALUE '*'.
           05  FILLER                PIC X(08)   VALUE 'SUCURSAL'.
           05  FILLER                PIC X(03)   VALUE ' : '.
           05  WS-COR-SUCURSAL       PIC X(02)   VALUE SPACES.
           05  FILLER                PIC X(03)   VALUE '   '.
           05  FILLER                PIC X(07)   VALUE 'BALANCE'.
           05  FILLER                PIC X(03)   VALUE ' : '.
           05  WS-COR-BALANCE        PIC -ZZZ.ZZZ.ZZ9,99
                                                 VALUE ZEROS.
           05  FILLER                PIC X(07)   VALUE SPACES.
           05  FILLER                PIC X(01)   VALUE '*'.

       77  FILLER       PIC X(26) VALUE '* FINAL WORKING-STORAGE *'.

      ***************************************
       PROCEDURE DIVISION.
      ***********************************
      *                                 *
      * CUERPO PRINCIPAL DEL PROGRAMA   *
      *                                 *
      ***********************************
       MAIN-PROGRAM.

            PERFORM 1000-INICIO

            PERFORM 2000-PROCESO
                    UNTIL FS-FIN-E1

            PERFORM 9000-FINAL

            GOBACK.

      **********************************
      *              INICIO            *
      **********************************
       1000-INICIO.

            INITIALIZE WS-VARIABLES

            PERFORM  1100-OPEN-FILES

            PERFORM  4100-LEER-ENTRA

            MOVE     CLIS-SUC         TO  WS-SUC-ANT
            IF FS-FIN-E1
               DISPLAY '* ARCHIVO ENTRADA VACIO'
            END-IF
            .
       1000-INICIO-F. EXIT.

       1100-OPEN-FILES.

            OPEN INPUT ENTRADA
            IF FS-ENTRADA  IS NOT EQUAL '00'
               MOVE  '1100-OPEN-FILES'         TO  WS-ERR-PARRAFO
               MOVE  'OPEN ENTRADA'            TO  WS-ERR-OPERACION
               MOVE  FS-ENTRADA                TO  WS-ERR-CODE
               PERFORM  9999-CANC-CTRL
            END-IF

            OPEN OUTPUT SALIDA
            IF FS-SALIDA   IS NOT EQUAL '00'
               MOVE  '1100-OPEN-FILES'         TO  WS-ERR-PARRAFO
               MOVE  'OPEN SALIDA'             TO  WS-ERR-OPERACION
               MOVE  FS-SALIDA                 TO  WS-ERR-CODE
               PERFORM  9999-CANC-CTRL
            END-IF
            .
      ****************************************
      *            PROCESO                   *
      ****************************************
       2000-PROCESO.
      *
           IF  CLIS-SUC = WS-SUC-ANT
               ADD 1 TO  WS-CONT-SUC
               COMPUTE   WS-ACC-BALANCE = WS-ACC-BALANCE + CLIS-IMPORTE
               PERFORM   5100-GARBAR-REG
               PERFORM   4100-LEER-ENTRA
           ELSE
               PERFORM  2100-CORTE-SUC
           END-IF
           .

       2100-CORTE-SUC.
      *
           MOVE WS-SUC-ANT       TO  WS-COR-SUCURSAL
           MOVE WS-ACC-BALANCE   TO  WS-COR-BALANCE
           PERFORM 5110-GRABAR-CORTE
           DISPLAY ' SUCURSAL : ' WS-SUC-ANT
                   ' CANTIDAD : ' WS-CONT-SUC
                   ' BALANCE  : ' WS-COR-BALANCE
           MOVE 0                TO  WS-CONT-SUC
           MOVE 0                TO  WS-ACC-BALANCE
           MOVE CLIS-SUC         TO  WS-SUC-ANT
           .
       4100-LEER-ENTRA.
      *
           READ ENTRADA                    INTO  REG-CLIENTES
           EVALUATE FS-ENTRADA
               WHEN '00'
                    ADD   1                  TO  WS-CONT-LEIDOS
               WHEN '10'
                    PERFORM  2100-CORTE-SUC
               WHEN OTHER
                    MOVE '4100-LEER-ENTRA'   TO  WS-ERR-PARRAFO
                    MOVE 'LEER ENTRADA   '   TO  WS-ERR-OPERACION
                    MOVE FS-ENTRADA          TO  WS-ERR-CODE
                    PERFORM 9999-CANC-CTRL
           END-EVALUATE
           .
       5100-GARBAR-REG.
      *
                MOVE   REG-CLIENTES   TO        REG-SALIDA
                WRITE  REG-SALIDA     FROM  REG-CLIENTES

           EVALUATE FS-SALIDA
               WHEN '00'
                    ADD   1                  TO  WS-CONT-GRABADOS
               WHEN OTHER
                    MOVE '5100-GARBAR-REG'   TO  WS-ERR-PARRAFO
                    MOVE 'GRABAR SALIDA  '   TO  WS-ERR-OPERACION
                    MOVE FS-SALIDA           TO  WS-ERR-CODE
                    PERFORM 9999-CANC-CTRL
           END-EVALUATE
           .
       5110-GRABAR-CORTE.
      *
           PERFORM  5111-GRABAR-LINEA
           WRITE    REG-SALIDA             FROM  WS-REG-CORTE
           EVALUATE FS-SALIDA
               WHEN '00'
                    ADD   1                  TO  WS-CONT-GRABADOS
               WHEN OTHER
                    MOVE '5110-GRABAR-CORTE' TO  WS-ERR-PARRAFO
                    MOVE 'GRABAR SALIDA  '   TO  WS-ERR-OPERACION
                    MOVE FS-SALIDA           TO  WS-ERR-CODE
                    PERFORM 9999-CANC-CTRL
           END-EVALUATE
           PERFORM  5111-GRABAR-LINEA
           .
       5111-GRABAR-LINEA.
      *
           WRITE  REG-SALIDA               FROM  WS-CONS-LINEA
           EVALUATE FS-SALIDA
               WHEN '00'
                    ADD   1                  TO  WS-CONT-GRABADOS
               WHEN OTHER
                    MOVE '5111-GRABAR-LINEA' TO  WS-ERR-PARRAFO
                    MOVE 'GRABAR SALIDA  '   TO  WS-ERR-OPERACION
                    MOVE FS-SALIDA           TO  WS-ERR-CODE
                    PERFORM 9999-CANC-CTRL
           END-EVALUATE
           .
      **************************************
      *  CUERPO FINAL CIERRE DE FILES      *
      **************************************
       9000-FINAL.
      *
           PERFORM 9100-CLOSE-FILES
           PERFORM 9200-MOSTRAR-TOTALES
           .
       9100-CLOSE-FILES.
      *
           CLOSE ENTRADA
              IF FS-ENTRADA IS NOT EQUAL '00'
                 MOVE '9100-CLOSE-FILES'  TO  WS-ERR-PARRAFO
                 MOVE 'CLOSE FILES    '   TO  WS-ERR-OPERACION
                 MOVE FS-ENTRADA          TO  WS-ERR-CODE
                 PERFORM 9999-CANC-CTRL
              END-IF

           CLOSE  SALIDA
              IF FS-SALIDA IS NOT EQUAL '00'
                 MOVE '9100-CLOSE-FILES'  TO  WS-ERR-PARRAFO
                 MOVE 'CLOSE FILES    '   TO  WS-ERR-OPERACION
                 MOVE FS-ENTRADA          TO  WS-ERR-CODE
                 PERFORM 9999-CANC-CTRL
              END-IF
              .
       9200-MOSTRAR-TOTALES.
           DISPLAY 'TOTALES LEIDOS   : '  WS-CONT-LEIDOS
           DISPLAY 'TOTALES GRABADOS : '  WS-CONT-GRABADOS
           .
       9999-CANC-CTRL.
           DISPLAY 'PARRAFO   '  WS-ERR-PARRAFO
           DISPLAY 'OPERACION '  WS-ERR-OPERACION
           DISPLAY 'CODIGO    '  WS-ERR-CODE
           COMPUTE CONS-CERO = CONS-CERO / CONS-CERO
           .
